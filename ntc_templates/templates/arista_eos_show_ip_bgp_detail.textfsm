Value LOCAL_PREF (\d+)
Value WEIGHT (\S+)
Value ORIGIN (.+)
Value Filldown PREFIX (\S+)
Value AS_PATH (\d+(\s.*?)*(\s.?)*|\d+\.\d+(\s.*?)*)
Value VALID (\S+)
Value PATH_TYPE (\S+)
Value ACTIVE (best)
Value COMMUNITY ((?:(?:\d+\s?\:\s?\d+\s?)|(?:internet|no\-export|no\-advertise|local\-as)\s?\d+\s?\:\s?\d+\s?)*)
Value NEIGHBOR (\d+\.\d+\.\d+\.\d+)
Value NEIGHBOR_ID (\S+)
Value Filldown ROUTER_ID (\S+)
Value Filldown LOCAL_AS (\S+)
				
Start
  ^BGP\s+routing\s+table\sinformation.+
  ^Router\s+identifier\s+${ROUTER_ID},\s+local\s+AS\s+number\s+${LOCAL_AS} -> Path

Path
  ^BGP\s+routing\s+table\s+entry\s+for\s+${PREFIX}
  ^\s+Paths:\s+\d+\s+available
  ^\s+Local$$
  ^\s+${AS_PATH}$$
  ^\s+\-\s+from\s+\S+\s+\(${NEIGHBOR_ID}\)
  ^\s+${NEIGHBOR}\s+from\s+\S+\s+\(${NEIGHBOR_ID}\)
  ^\s+Origin\s+${ORIGIN},\s+metric\s+\d+,\s+localpref\s+${LOCAL_PREF},\s+IGP\s+metric\s+\S+,\s+weight\s+\-,\s+received\s+\S+\s+ago,\s+${VALID},\s+${PATH_TYPE},\s+${ACTIVE}
  ^\s+Origin\s+${ORIGIN},\s+metric\s+\d+,\s+localpref\s+${LOCAL_PREF},\s+IGP\s+metric\s+\S+,\s+weight\s+${WEIGHT},\s+received\s+\S+\s+ago,\s+${VALID},\s+${PATH_TYPE},\s+${ACTIVE}
  ^\s+Origin\s+${ORIGIN},\s+metric\s+\d+,\s+localpref\s+${LOCAL_PREF},\s+IGP\s+metric\s+\S+,\s+weight\s+${WEIGHT},\s+received\s+\S+\s+ago,\s+${VALID},\s+${PATH_TYPE}
  ^\s+Community:\s+${COMMUNITY}$$
  ^\s+Extended\s+Community:\s+\S+
  ^\s+Rx\s+SAFI:\s+\S+ -> Record
  ^. -> Error

EOF
